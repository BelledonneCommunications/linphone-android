// Project information
buildDir = 'bin'

def getPackageName() {
  return "org.linphone.tester"
}

def firebaseEnable() {
  File googleFile = new File('google-services.json')
  return googleFile.exists()
}

buildscript {
  File googleFile = new File('google-services.json')
  repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:2.2.0'
    if (googleFile.exists()) {
      classpath 'com.google.gms:google-services:3.0.0'
    }
  }
}

apply plugin: 'com.android.application'
if (firebaseEnable()) {
  apply plugin: 'com.google.gms.google-services'
}


dependencies {
  compile fileTree(dir: 'libs', include: '*.jar')
  if (firebaseEnable()) {
    compile 'com.google.firebase:firebase-messaging:9.0.0'
  } else {
    compile 'com.android.support:support-v4:+'
  }
}

android {
  defaultConfig {
    compileSdkVersion 23
    buildToolsVersion "25.0.2"
    applicationId getPackageName()
    multiDexEnabled true
  }

  sourceSets {
    main {
      String firebase = ''
      // Exclude firebase file if not enable
      if (!firebaseEnable()) {
        firebase = '**/Firebase*'
        println '[Push Notification] Firebase disabled'
      } else {
        println '[Push Notification] Firebase enabled'
      }

      manifest.srcFile 'AndroidManifest.xml'
      java.srcDirs = ['../submodules/linphone/mediastreamer2/java/src','../submodules/linphone/java/j2se','../submodules/linphone/java/common','../submodules/linphone/java/impl','../submodules/externals/axmlrpc/src/main/java','../submodules/linphone/coreapi/help/java','src']
      resources.srcDirs = ['../submodules/linphone/mediastreamer2/java/src','../submodules/linphone/java/j2se','../submodules/linphone/java/common','../submodules/linphone/java/impl','../submodules/externals/axmlrpc/src/main/java','../submodules/linphone/coreapi/help/java','src']
      aidl.srcDirs = ['../submodules/linphone/mediastreamer2/java/src','../submodules/linphone/java/j2se','../submodules/linphone/java/common','../submodules/linphone/java/impl','../submodules/externals/axmlrpc/src/main/java','../submodules/linphone/coreapi/help/java','src']
      renderscript.srcDirs = ['../submodules/linphone/mediastreamer2/java/src','../submodules/linphone/java/j2se','../submodules/linphone/java/common','../submodules/linphone/java/impl','../submodules/externals/axmlrpc/src/main/java','../submodules/linphone/coreapi/help/java','src']
      res.srcDirs = ['res']
      assets.srcDirs = ['assets']
      jniLibs.srcDir 'libs'

      java.exclude firebase
    }
  }

  lintOptions {
    checkReleaseBuilds false
    // Or, if you prefer, you can continue to check for errors in release builds,
    // but continue the build even when errors are found:
    abortOnError false
  }
}
